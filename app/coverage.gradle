apply plugin: 'jacoco'

jacoco {
    toolVersion = "0.7.7.201606060606"
}

def coverageSourceDirs = files(['src/main/java'])
def coverageClassDirs = fileTree(
        dir: "${buildDir}/intermediates/classes/debug",
        excludes: ['**/R.class',
                   '**/R$*.class',
                   '**/BuildConfig.*',
                   '**/Manifest*.*',
                   'com/android/**/*.class',
                   '**/*_ViewBind*' // ButterKnife auto generated classes
        ]
)

def unitTestCoverageData = "${buildDir}/jacoco/testDebugUnitTest.exec"
def instrumentationTestCoverageData = "${buildDir}/spoon/debug/coverage/merged-coverage.ec"

// testCoverageEnabled messes up debugging/etc, so we want it disabled most of times.
// unit test coverage reports seems to work fine even with the flag disabled, but the
// instrumentation tests need this flag otherwise the coverage data may be incomplete
def startTaskNames = gradle.startParameter.taskNames
def isRunningInstrumentationCoverage = startTaskNames.contains('instrumentationTestCoverage') || startTaskNames.contains('testCoverage')

// Run both unit and instrumentation tests and merge the result of both tests into a single report
task testCoverage(type: JacocoReport, dependsOn: ['testDebugUnitTest', 'spoonDebugAndroidTest']) {
    sourceDirectories = coverageSourceDirs
    classDirectories = coverageClassDirs
    executionData = files([unitTestCoverageData, instrumentationTestCoverageData])

    doLast {
        println "You can view the coverage report at:"
        println "${buildDir}/reports/jacoco/testCoverage/html/index.html"
    }

    reports {
        xml.enabled true
        xml.destination "${buildDir}/reports/jacoco/testCoverage/coverage.xml"
    }
}

// Run and generate the report for the unit tests. The test report only includes the coverage data for
// the presenter and utils classes since we generally don't unit test any Android related classes
task unitTestCoverage(type: JacocoReport, dependsOn: 'testDebugUnitTest') {
    sourceDirectories = coverageSourceDirs
    classDirectories = fileTree(
            dir: "${buildDir}/intermediates/classes/debug",
            includes: ['**/*Presenter*',
                       '**/utils/*'
                       // Add any additional testable classes here
            ]
    )
    executionData = files("${buildDir}/jacoco/testDebugUnitTest.exec")

    doLast {
        println "You can view the coverage report at:"
        println "${buildDir}/reports/jacoco/unitTestCoverage/html/index.html"
    }

    reports {
        xml.enabled true
        xml.destination "${buildDir}/reports/jacoco/unitTestCoverage/coverage.xml"
    }
}

// Run and generate the report for the instrumentation test
task instrumentationTestCoverage(type: JacocoReport, dependsOn: 'spoonDebugAndroidTest') {
    sourceDirectories = coverageSourceDirs
    classDirectories = coverageClassDirs
    executionData = files(instrumentationTestCoverageData)

    doLast {
        println "You can view the coverage report at:"
        println "${buildDir}/reports/jacoco/instrumentationTestCoverage/html/index.html"
    }

    reports {
        xml.enabled true
        xml.destination "${buildDir}/reports/jacoco/instrumentationTestCoverage/coverage.xml"
    }
}

android {
    buildTypes {
        debug {
            testCoverageEnabled(isRunningInstrumentationCoverage)
        }
    }

    testOptions {
        unitTests.all {
            jacoco {
                // needed so that Robolectric tests are included in the results
                includeNoLocationClasses = true
            }
        }
    }
}

spoon {
    codeCoverage = isRunningInstrumentationCoverage
}
